breakpoint;
alias functionNum R1;
alias currentPID R2;

//Acquire Disk
if(functionNum==3)then
    while([DISK_STATUS_TABLE] == 1)do
        [PROCESS_TABLE + currentPID*16 + 4] = WAIT_DISK;
        multipush(R1,R2);
        call MOD_5;
        multipop(R1,R2);
    endwhile;
    [DISK_STATUS_TABLE] = 1;
    [DISK_STATUS_TABLE + 4] = currentPID;
    breakpoint;
    return;
endif;

//Acquire Inode
if(functionNum==4)then
    alias inodeIndex R2;
    alias currentPID R3;
    //while inode is locked, set State as WAIT_FILE and invoke scheduler
    while([FILE_STATUS_TABLE + inodeIndex*4] != -1)do
        [PROCESS_TABLE + currentPID*16 + 4] = WAIT_FILE;
        [PROCESS_TABLE + currentPID*16 + 5] = inodeIndex;
        multipush(R1,R2,R3);
        call MOD_5;
        multipop(R1,R2,R3);
    endwhile;

    //If inode becomes invalid(Deleted), return -1
    if([INODE_TABLE + inodeIndex*16 + 1] == -1)then
        R0 = -1;
        breakpoint;
        return;
    endif;

    //Lock Inode by setting the Locking PID in File Status Table
    [FILE_STATUS_TABLE + inodeIndex*4] = currentPID;
    //return 0
    R0 = 0;
    breakpoint;
    return;
endif;

//Release Inode
if(functionNum==5)then
    alias inodeIndex R2;
    alias currentPID R3;
    //If PID given as input not equal to locking PID, return -1
    if([FILE_STATUS_TABLE + inodeIndex*4] != currentPID) then
        R0 = -1;
        breakpoint;
        return;
    endif;

    //Free the lock(Set it to -1)
    [FILE_STATUS_TABLE + inodeIndex*4] = -1;

    //Wake up processes waiting for that file
    R4 = 0;
    while(R4 < 16)do
        if([PROCESS_TABLE + R4*16 + 4] == WAIT_FILE && [PROCESS_TABLE + R4*16 + 5] == inodeIndex) then
            [PROCESS_TABLE + R4*16 + 4] = READY;
            [PROCESS_TABLE + R4*16 + 5] = -1;
        endif;
        R4 = R4 + 1;
    endwhile;

    //Return 0
    R0 = 0;
    breakpoint;
    return;
endif;

//Acquire Semaphore
if(functionNum==6)then
    //Find Index of free Entry in Semaphore Table. If no entry,return -1
    R3 = 0;
    while(R3 < MAX_SEM_COUNT) do
        if([SEMAPHORE_TABLE + R3*4 + 1]==0) then
            break;
        endif;
        R3 = R3 + 1;
    endwhile;
    //If no free entry, return -1
    if(R3 == MAX_SEM_COUNT) then
        R0 = -1;
        return;
    endif;
    //Set Process Count and Locking PID
    [SEMAPHORE_TABLE + R3*4] = -1;
    [SEMAPHORE_TABLE + R3*4 + 1] = 1;
    //return Semaphore Table Index;
    R0 = R3;
    breakpoint;
    return;
endif;

//Release Semaphore
if(functionNum==7)then
    //If locking PID is current Process
    if([SEMAPHORE_TABLE + R2*4] == R3)then
        //Unlock Semaphore
        [SEMAPHORE_TABLE + R2*4] = -1;
        R4 = 0;
        //Wake up processes blocked by semaphore
        while(R4 < 16)do
            if([PROCESS_TABLE + R4*16 + 4] == WAIT_SEMAPHORE && [PROCESS_TABLE + R4*16 + 5] == R2)then
                [PROCESS_TABLE + R4*16 + 4] = READY;
                [PROCESS_TABLE + R4*16 + 5] = -1;
            endif;
            R4 = R4 + 1;
        endwhile;
    endif;
    //Decrement process Count
    [SEMAPHORE_TABLE + R2*4 + 1] = [SEMAPHORE_TABLE + R2*4 + 1] - 1;
    breakpoint;
    return;
endif;

//Acquire Terminal
if(functionNum==8)then
    while([TERMINAL_STATUS_TABLE] == 1)do
        [PROCESS_TABLE + currentPID*16 + 4] = WAIT_TERMINAL;
        multipush(R1,R2);
        call MOD_5;
        multipop(R1,R2);
    endwhile;
    [TERMINAL_STATUS_TABLE] = 1;
    [TERMINAL_STATUS_TABLE + 1] = currentPID;
    breakpoint;
    return;
endif;

//Release Terminal
if(functionNum == 9)then
    if(currentPID != [TERMINAL_STATUS_TABLE + 1])then
        R0 = -1;
        return;
    endif;
    [TERMINAL_STATUS_TABLE] = 0;
    R3 = 1;
    while(R3 < 16) do
        if([PROCESS_TABLE + R3*16 + 4]==WAIT_TERMINAL)then
            [PROCESS_TABLE + R3*16 + 4] = READY;
        endif;
        R3 = R3 + 1;
    endwhile;
    R0 = 0;
    breakpoint;
    return;
endif;

return;