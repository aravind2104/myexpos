//Init
loadi(65,7);
loadi(66,8);
//Shell
loadi(67,9);
loadi(68,10);
//Exception
loadi(2,15);
loadi(3,16);
//library
loadi(63,13);
loadi(64,14);
//Timer
loadi(4,17);
loadi(5,18);
//Disk ISR(INT 2)
loadi(6,19);
loadi(7,20);
//Console(INT 3)
loadi(8,21);
loadi(9,22);
//INT 4
loadi(10,23);
loadi(11,24);
//INT 5
loadi(12,25);
loadi(13,26);
//INT 6
loadi(14,27);
loadi(15,28);
//INT 7
loadi(16,29);
loadi(17,30);
//INT 8(fork)
loadi(18,31);
loadi(19,32);
//INT 9
loadi(20,33);
loadi(21,34);
//INT 10
loadi(22,35);
loadi(23,36);
//INT 11
loadi(24,37);
loadi(25,38);
//INT 12
loadi(26,39);
loadi(27,40);
//INT 13
loadi(28,41);
loadi(29,42);
//INT 14
loadi(30,43);
loadi(31,44);
//INT15
loadi(32,45);
loadi(33,46);
//INT 16
loadi(34,47);
loadi(35,48);
//INT 17
loadi(36,49);
loadi(37,50);
//Module 0
loadi(40,53);
loadi(41,54);
//Module 1
loadi(42,55);
loadi(43,56);
//Module 2
loadi(44,57);
loadi(45,58);
//Module 3
loadi(46,59);
loadi(47,60);
//Module 4
loadi(48,61);
loadi(49,62);
//Module 5
loadi(50,63);
loadi(51,64);
//Inode Table
loadi(59,3);
loadi(60,4);
//Disk Free list
loadi(61,2);
//Root File
loadi(62,5);

[TERMINAL_STATUS_TABLE] = 0;

[DISK_STATUS_TABLE] = 0;

//Initializing Memory Free list
//Till Page 75 is reserved for OS
alias i R0;
i = 0;
while(i<=80) do
    [MEMORY_FREE_LIST + i]=1;
    i=i+1;
endwhile;
i = 81;
while(i<128) do
    [MEMORY_FREE_LIST + i]=0;
    i=i+1;
endwhile;

//Initiailize WAIT_MEM_COUNT to 0 and MEM_FREE_COUNT to no of free pages
[SYSTEM_STATUS_TABLE + 3] = 0;
[SYSTEM_STATUS_TABLE + 2] = 47; //127-80

//Semaphore Table
breakpoint;
i = 0;
while(i<128) do //128 because 32(Max_Sem_Count)*4(Size of one Entry)=128
    [SEMAPHORE_TABLE + i] = -1;
    [SEMAPHORE_TABLE + i + 1] = 0;
    i = i + 4;
endwhile;

//Initiailize File Status Table
i = 0;
while(i < 60) do
    [FILE_STATUS_TABLE + 4*i] = -1; //Locking PID
    [FILE_STATUS_TABLE + 4*i + 1] = -1; //File Open Count
    i = i + 1;
endwhile;

//Initiailize Buffer Table
i = 0;
while(i < 4) do
    [BUFFER_TABLE + i*4] = -1; //Block Number
    [BUFFER_TABLE + i*4 + 1] = 0; //Dirty Bit
    [BUFFER_TABLE + i*4 + 2] = -1; //Locking PID
    i = i + 1;
endwhile;

//Initiailize Open File Table
i = 0;
while(i<MAX_OPENFILE_NUM)do
    [OPEN_FILE_TABLE + i*4] = -1; //Inode Index
    [OPEN_FILE_TABLE + i*4 + 1] = 0; //Open Instance Count
    i = i + 1;
endwhile;

//Init Process with PID=1
PTBR=PAGE_TABLE_BASE+20;
PTLR=10;

//Library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

//Heap
[PTBR+4] = -1;
[PTBR+5] = "0000";
[PTBR+6] = -1;
[PTBR+7] = "0000";

//Code
[PTBR+8] = 65;
[PTBR+9] = "0100";
[PTBR+10] = -1;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

//Stack
[PTBR+16] = 66;
[PTBR+17] = "0110";
[PTBR+18] = -1;
[PTBR+19] = "0000";

//Process Table
[PROCESS_TABLE + 16 + 1] = 1; //PID
[PROCESS_TABLE + 16 + 3] = 1; //PPID
[PROCESS_TABLE + 16 + 4] = CREATED; //STATE
[PROCESS_TABLE + 16 + 11] = 77; //User Area Page
[PROCESS_TABLE + 16 + 12] = 0; //KTPR
[PROCESS_TABLE + 16 + 13] = 8*512; //UTPR
[PROCESS_TABLE + 16 + 14] = PTBR; //PTBR
[PROCESS_TABLE + 16 + 15] = 10; //PTLR

//Per Process Table for INIT process (-1 in this stage)
i = 496;
while(i < 512)do
    [80*512 + i] = -1;
    i = i + 1;
endwhile;

[66*512] = [65*512 + 1];

//Shell Process(PID = 2)
//Init Process with PID=1
PTBR=PAGE_TABLE_BASE+20*2;
PTLR=10;

//Library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

//Heap
[PTBR+4] = -1;
[PTBR+5] = "0000";
[PTBR+6] = -1;
[PTBR+7] = "0000";

//Code
[PTBR+8] = 67;
[PTBR+9] = "0100";
[PTBR+10] = 68;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

//Stack
[PTBR+16] = 78;
[PTBR+17] = "0110";
[PTBR+18] = 79;
[PTBR+19] = "0110";

//Process Table
[PROCESS_TABLE + 16*2 + 1] = 2; //PID
[PROCESS_TABLE + 16*2 + 3] = 1; //PPID
[PROCESS_TABLE + 16*2 + 4] = TERMINATED; //STATE
[PROCESS_TABLE + 16*2 + 11] = 80; //User Area Page
[PROCESS_TABLE + 16*2 + 12] = 0; //KTPR
[PROCESS_TABLE + 16*2 + 13] = 8*512; //UTPR
[PROCESS_TABLE + 16*2 + 14] = PTBR; //PTBR
[PROCESS_TABLE + 16*2 + 15] = 10; //PTLR

//Storing starting IP address
[78*512] = [67*512 + 1];

//PER PROCESS DISK MAP TABLE 
//Initiailizing Disk Map Table entries for Shell process
i = 0;
while(i<10) do 
    [DISK_MAP_TABLE + 10*2 + i] = -1;
    i = i + 1;
endwhile;
[DISK_MAP_TABLE + 10*2 + 4] = 9;
[DISK_MAP_TABLE + 10*2 + 5] = 10;

alias id R1;
id = 2;
while(id<=15) do
    [PROCESS_TABLE + 16*id + 4] = TERMINATED;
    id = id+1;
endwhile;

return;
